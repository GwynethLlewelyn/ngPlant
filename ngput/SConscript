from sctool.SConcompat import *

NGPUT_SRC = Split("""
p3dimage.cpp
p3dimagetga.cpp
p3dospath.cpp
p3dglext.cpp
p3dglmemcntx.cpp
""")

NGPUTIMG_SRC = []

NGPUT_INCLUDES = Split("""
#
""")

Import('*')

NGPUtEnv = EnvClone(BaseEnv)
NGPUtEnv.Append(CPPPATH=NGPUT_INCLUDES)

print('With LIBPNG:', NGPUtEnv['WITH_LIBPNG'])
print('With LIBJPEG:', NGPUtEnv['WITH_LIBJPEG'])

if CC_WARN_FLAGS != '':
    NGPUtEnv.Append(CXXFLAGS=CC_WARN_FLAGS)
if CC_OPT_FLAGS != '':
    NGPUtEnv.Append(CXXFLAGS=CC_OPT_FLAGS)

NGPUtEnv.Append(CPPPATH=NGPUtEnv['GLEXT_INC'])

if NGPUtEnv['WITH_LIBPNG']:
    NGPUtEnv.Append(CPPPATH=NGPUtEnv['LIBPNG_CPPPATH'])
    NGPUtEnv.Append(CPPDEFINES=NGPUtEnv['LIBPNG_CPPDEFINES'])

    print('LIBPNG_CPPPATH is: ', NGPUtEnv['LIBPNG_CPPPATH'])
    print('LIBPNG_CPPDEFINES is: ', NGPUtEnv['LIBPNG_CPPDEFINES'])

    NGPUTIMG_SRC.append('p3dimagepng.cpp')

if NGPUtEnv['WITH_LIBJPEG']:
    NGPUtEnv.Append(CPPPATH=NGPUtEnv['LIBJPEG_CPPPATH'])
    NGPUtEnv.Append(CPPDEFINES=NGPUtEnv['LIBJPEG_CPPDEFINES'])

    print('LIBJPEG_CPPPATH is: ', NGPUtEnv['LIBJPEG_CPPPATH'])
    print('LIBJPEG_CPPDEFINES is: ', NGPUtEnv['LIBJPEG_CPPDEFINES'])

    NGPUTIMG_SRC.append('p3dimagejpg.cpp')

ngput = NGPUtEnv.StaticLibrary(target='ngput',source=NGPUT_SRC)

if len(NGPUTIMG_SRC) > 0:
    ngputimg = NGPUtEnv.StaticLibrary(target='ngputimg',source=NGPUTIMG_SRC)

Default(ngput)
Clean(ngput,['.sconsign'])

